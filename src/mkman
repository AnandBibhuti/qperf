#!/usr/bin/env perl
# Make qperf man page.
#
use strict;
use warnings;
use diagnostics;


my $help_txt  = "help.txt";


sub panic {
    print STDERR @_, "\n";
    exit 1;
}


sub printx {
    print STDOUT "@_";
}


sub printn {
    print STDOUT "@_\n";
}


sub printh ($) {
    my $name = shift;
    printn ".SH ", uc $name;
}


sub cleanup {
    my $str = shift;
    $str =~ s/^    //gm;
    return $str;
}


sub do_tests ($$) {
    my $dict = shift;
    my $name = shift;
    my $text = $dict->{"Tests +RDMA"};
    printh $name;
    $text =~ s/^\s*//;
    my @lines = split(/\n/, $text);
    for (@lines) {
        next unless /^ {8}(\w+)\s+(.*)/;
        printn '.TP';
        printn "\\fB$1\\fP";
        printn $2;
    }
}


sub do_options ($$) {
    my $dict = shift;
    my $name = shift;
    my $text = $dict->{$name};
    printh $name;
    $text =~ s/^\s*//;
    my @options = split(/^\s{4,6}(?=--)/m, $text);
    for my $option (@options) {
        $option =~ s/(.*)\n\s*//;
        my $head = $1;
        my $line = "";
        ($head =~ s/(.*)\s+\((-\w+)\)/$1/) and
            $line = "\\fB$2\\fP, ";
        $head =~ /(\S+)\s*(.*)/;
        $line .= "\\fB$1\\fP";
        $line .= " \\fI$2\\fP" if $2;
        printn '.TP';
        printn $line;
        $option =~ s/^\s+//mg;
        $option =~ s/\s+$//;
        printn $option;
    }
}


sub do_examples ($$) {
    my $dict = shift;
    my $name = shift;
    my $text = $dict->{$name};
    printh $name;
    my @lines = split(/^ {8}\* /m, $text);
    printx cleanup(shift @lines);
    for (@lines) {
        s/(.*)\n//;
        printn '.TP';
        printn $1;
        s/^\s+//m;
        print $_;
    }
}


sub do_synopsis ($$) {
    my $dict = shift;
    my $name = shift;
    my $text = $dict->{$name};
    printh $name;
    $text =~ s/([A-Z]+)/\\fI$1\\fP/g;
    $text =~ s/(qperf)/\\fB$1\\fP/g;
    $text =~ s/\n/\n.br\n/g;
    printx cleanup $text;
}


sub do_general ($$) {
    my $dict = shift;
    my $name = shift;
    printh $name;
    printx cleanup $dict->{$name};
}


sub make_dict ($) {
    my %dict;
    my $data = shift;
    my @entrys = split(/^(?=\S)/m, $data);
    (s/^(.*)\n//) and
        $dict{$1} = $_
            for (@entrys);
    return \%dict;
}


sub read_file ($) {
    my $name = shift;
    my $file;
    local $/;
    open($file, "<", $name) or
        panic("cannot find $name");
    my $data = <$file>;
    close $file;
    return $data;
}


sub main() {
    my $data = read_file $help_txt;
    my $help = make_dict $data;
    my $main = make_dict cleanup $help->{Main};

    printn '.\" Generated by mkman';
    printn '.TH QPERF 1 "October 2007" "qperf" "User Commands"';
    printn '.SH NAME';
    printn 'qperf \- Measure RDMA and IP performance>';

    do_synopsis ($main, 'Synopsis');
    do_general  ($main, 'Description');
    do_examples ($help, 'Examples');
    do_options  ($help, 'Options');
    do_tests    ($help, 'Tests');
    do_general  ($help, 'Author');
    do_general  ($help, 'Bugs');
}

main();
